buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath group: 'com.stanfy.android', name: 'gradle-plugin-java-robolectric', version : '2.0'
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
    classpath 'com.android.tools.build:gradle:0.3'
  }
}

def getPackageName = { "com.stanfy.enroscar.${it.name.replaceAll(/\W/, '')}" }
def projectVersion = "0.9-SNAPSHOT"

def androidLibs = ['sdk-dep']

// Java Projects

configure(subprojects.findAll() { !(it.name in androidLibs) }) {
  
  apply plugin : 'java-robolectric'
  apply plugin : 'nexus'
  
  sourceCompatibility = 1.6

  javarob {
    packageName = getPackageName(project)
  }
  
  dependencies {
    compile group : 'com.google.android', name : 'android', version : '4.1.1.4'
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile group: 'org.robolectric', name: 'robolectric', version: '2.0-alpha-2'
  }

  test {
    scanForTestClasses = false
    include "**/*Test.class"
    exclude "**/Abstract*.class"
  }
  
  task testsJar(type : Jar) {
    classifier = 'tests'
    from sourceSets.test.output
  }
  
  configurations {
    testArchives {
      extendsFrom testRuntime
    }
  }
  
  artifacts {
    testArchives testsJar
  }
  
  modifyPom {
    project {
      name 'Enroscar'
      description 'Android Library'
      url 'https://github.com/stanfy/gson-xml'
      inceptionYear '2012'
  
      scm {
        url 'https://github.com/stanfy/enroscar'
        connection 'scm:git:git@github.com:stanfy/enroscar.git'
        developerConnection 'scm:git:git@github.com:stanfy/enroscar.git'
      }
  
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
  
    }
  }

}

// Android Projects

configure(subprojects.findAll() { it.name in androidLibs }) {
  
//  apply plugin : 'android-library'
//
//  android {
//    compileSdkVersion 17
//    
//    defaultConfig {
//      versionCode 9
//      versionName projectVersion
//      minSdkVersion 8
//      targetSdkVersion 17
//      packageName getPackageName(project)
//    }
//  }

}

// ==================================== COMMON ====================================

def checkstyleDir = new File(rootDir, 'config/checkstyle')
def checkstyleConfig = new File(checkstyleDir, 'checkstyle.xml')
  
subprojects {

  apply plugin : 'checkstyle'
  apply plugin : 'eclipse'
  apply plugin : 'maven'
  
  version = projectVersion
  group = 'com.stanfy.enroscar'
  
  checkstyle {
    configFile = checkstyleConfig
    configProperties['basedir'] = project.projectDir
  }
  
  repositories {
    mavenCentral()
  }
  
}
