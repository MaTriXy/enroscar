buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath group: 'com.stanfy.android', name: 'gradle-plugin-java-robolectric', version : '2.0'
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.2'
  }
}

def checkstyleDir = new File(rootDir, 'config/checkstyle')
def checkstyleConfig = new File(checkstyleDir, 'checkstyle.xml')
  
subprojects {
  
  apply plugin : 'checkstyle'
  apply plugin : 'eclipse'
  apply plugin : 'java-robolectric'
  apply plugin : 'nexus'
  
  sourceCompatibility = 1.6
  version = '0.9-SNAPSHOT'
  
  javarob {
    packageName = "com.stanfy.enroscar.${project.name}"
  }
  
  checkstyle {
    configFile = checkstyleConfig
    configProperties['basedir'] = project.projectDir
  }
  
  jar {
    manifest {
      attributes 'Implementation-Title': 'Enroscar Beans', 'Implementation-Version': version
    }
  }
  
  test {
    scanForTestClasses = false
    include "**/*Test.class"
  }
  
  task testsJar(type : Jar) {
    classifier = 'tests'
    from sourceSets.test.output
  }
  
  configurations {
    testArchives {
      extendsFrom testRuntime
    }
  }
  
  artifacts {
    testArchives testsJar
  }
  
  repositories {
    mavenCentral()
  }
  
  dependencies {
    compile 'com.google.android:android:4.1.1.4'
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile group: 'org.robolectric', name: 'robolectric', version: '2.0-alpha-2'
  }
  
  modifyPom {
    project {
      name 'Enroscar'
      description 'Android Library'
      url 'https://github.com/stanfy/gson-xml'
      inceptionYear '2012'
  
      scm {
        url 'https://github.com/stanfy/enroscar'
        connection 'scm:git:git@github.com:stanfy/enroscar.git'
        developerConnection 'scm:git:git@github.com:stanfy/enroscar.git'
      }
  
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
  
    }
  }

}
